plugins {
    id 'com.android.application'
}

android {
    namespace 'it.niedermann.nextcloud.tables'
    compileSdk 33

    defaultConfig {
        applicationId "it.niedermann.nextcloud.tables"
        minSdk 26
        targetSdk 33
        versionCode 1001002
        versionName "1.1.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"

    productFlavors {
        fdroid {
            dimension "version"
        }
        dev {
            dimension "version"
            applicationIdSuffix ".dev"
        }
        play {
            dimension "version"
            applicationIdSuffix ".play"
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources true
        }
    }
    lint {
        abortOnError false
        disable 'MissingTranslation'
    }
}

ext {
    version_nextcloud_commons = '1.8.0'
    version_glide = '4.16.0'
    version_room = '2.5.1'
    version_emoji = '1.3.0'
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'

    // Nextcloud SSO
    implementation 'com.github.nextcloud.android-common:ui:0.15.0'
    implementation 'com.github.nextcloud:Android-SingleSignOn:0.8.0'
    implementation 'com.github.stefan-niedermann:android-commons:0.3.1'
    implementation "com.github.stefan-niedermann.nextcloud-commons:sso-glide:$version_nextcloud_commons"
    implementation "com.github.stefan-niedermann.nextcloud-commons:exception:$version_nextcloud_commons"
    implementation("com.github.stefan-niedermann.nextcloud-commons:markdown:$version_nextcloud_commons") {
        exclude group: 'org.jetbrains', module: 'annotations-java5'
    }

    // Database
    implementation "androidx.room:room-runtime:$version_room"
    annotationProcessor "androidx.room:room-compiler:$version_room"

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    // Glide
    implementation "com.github.bumptech.glide:glide:$version_glide"
    annotationProcessor "com.github.bumptech.glide:compiler:$version_glide"

    // TableView
    implementation 'com.github.evrencoskun:TableView:v0.8.9.4'

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core-splashscreen:1.0.1'
    implementation "androidx.emoji2:emoji2:$version_emoji"
    implementation "androidx.emoji2:emoji2-views:$version_emoji"
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.work:work-runtime:2.8.1'
    implementation 'com.google.android.material:material:1.9.0'

    // Testing
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation 'androidx.arch.core:core-testing:2.2.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.10.0'
    testImplementation 'org.robolectric:robolectric:4.11.1'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
}